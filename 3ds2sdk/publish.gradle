apply(plugin: "signing")
apply(plugin: "maven-publish")

tasks.register("androidSourcesJar", Jar) {
    archiveClassifier.set("sources")
    from(android.sourceSets.main.java.srcDirs)
}

afterEvaluate {
    // See https://developer.android.com/studio/build/maven-publish-plugin
    // and https://docs.gradle.org/current/userguide/publishing_maven.html
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from(components.release)

                // You can then customize attributes of the publication as shown below.
                groupId = "com.stripe"
                artifactId = "stripe-3ds2-android"
                version = VERSION_NAME

                pom {
                    name = "stripe-3ds2-android"
                    packaging = "aar"
                    description = "Stripe 3DS2 Android SDK"
                    url = "https://github.com/stripe/stripe-android"

                    scm {
                        url = "https://github.com/stripe/stripe-android"
                        connection = "scm:org-856813@github.com:stripe/stripe-android.git"
                        developerConnection = "scm:org-856813@github.com:stripe/stripe-android.git"
                    }

                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://opensource.org/licenses/MIT"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "stripe"
                            name = "Stripe"
                        }
                    }
                }
            }
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("publish") }
        useGpgCmd()
        sign(publishing.publications.release)
    }

    tasks.withType(Sign).configureEach {
        onlyIf { project.hasProperty("signing.gnupg.keyName") }
    }

    artifacts {
        archives(androidSourcesJar)
    }
}
