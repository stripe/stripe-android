apply plugin: "signing"
apply plugin: "maven-publish"

def getSnapshotRepositoryUrl() {
    return findProperty('SNAPSHOT_REPOSITORY_URL') ?:
            "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getReleaseRepositoryUrl() {
    return findProperty('RELEASE_REPOSITORY_URL') ?:
            "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getRepositoryUsername() {
    return findProperty('NEXUS_USERNAME') ?: ""
}

def getRepositoryPassword() {
    return findProperty('NEXUS_PASSWORD') ?: ""
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    // See https://developer.android.com/studio/build/maven-publish-plugin
    // and https://docs.gradle.org/current/userguide/publishing_maven.html
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                artifact androidSourcesJar

                // You can then customize attributes of the publication as shown below.
                groupId = "com.stripe"
                artifactId = "stripe-3ds2-android"
                version = VERSION_NAME

                pom {
                    name = "stripe-3ds2-android"
                    packaging = "aar"
                    description = "Stripe 3DS2 Android SDK"
                    url = "https://github.com/stripe/stripe-android"

                    scm {
                        url = "https://github.com/stripe/stripe-android"
                        connection = "scm:org-856813@github.com:stripe/stripe-android.git"
                        developerConnection = "scm:org-856813@github.com:stripe/stripe-android.git"
                    }

                    licenses {
                        license {
                            name = "The MIT License"
                            url = "https://raw.githubusercontent.com/stripe/stripe-android/master/LICENSE"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "stripe"
                            name = "Stripe"
                        }
                    }
                }
            }
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("publish") }
        useGpgCmd()
        sign publishing.publications.release
    }

    tasks.withType(Sign) {
        onlyIf { project.hasProperty('signing.gnupg.keyName') }
    }

    artifacts {
        archives androidSourcesJar
    }
}
