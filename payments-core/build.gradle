apply from: configs.androidLibrary

apply plugin: 'kotlin-kapt'
apply plugin: 'checkstyle'
apply plugin: 'org.jetbrains.kotlin.plugin.parcelize'

dependencies {
    api project(":stripe-core")
    api project(":stripe-ui-core")
    api project(":payments-model")
    compileOnly project(':financial-connections')

    implementation "androidx.annotation:annotation:$androidxAnnotationVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.browser:browser:$androidxBrowserVersion"
    implementation "androidx.recyclerview:recyclerview:$androidxRecyclerviewVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$androidxLifecycleVersion"
    implementation "androidx.fragment:fragment-ktx:$androidxFragmentVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintlayoutVersion"
    implementation "androidx.activity:activity-ktx:$androidxActivityVersion"
    implementation "androidx.activity:activity-compose:$androidxActivityVersion"
    implementation "com.google.android.gms:play-services-wallet:$playServicesWalletVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
    implementation 'com.google.android.instantapps:instantapps:1.1.0'
    // For instructions on replacing the BouncyCastle dependency used by the 3DS2 SDK, see
    // https://github.com/stripe/stripe-android/issues/3173#issuecomment-785176608
    implementation "com.stripe:stripe-3ds2-android:$stripe3ds2Version"

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    javadocDeps "androidx.annotation:annotation:$androidxAnnotationVersion"
    javadocDeps "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    javadocDeps "com.google.android.material:material:$materialVersion"

    testImplementation project(':financial-connections')
    testImplementation project(':payments-core-testing')
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "androidx.test:core:$androidTestVersion"
    testImplementation "org.json:json:$jsonVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlinVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
    testImplementation "com.google.truth:truth:$truthVersion"
    testImplementation "androidx.arch.core:core-testing:$androidxArchCoreVersion"
    testImplementation "androidx.fragment:fragment-testing:$androidxFragmentVersion"
    testImplementation "androidx.lifecycle:lifecycle-runtime-testing:$androidxLifecycleVersion"
    testImplementation project(path: ':network-testing')
    testImplementation testLibs.turbine

    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"
    androidTestImplementation "androidx.test:runner:$androidTestRunnerVersion"

    androidTestUtil "androidx.test:orchestrator:$androidTestOrchestratorVersion"

    ktlint "com.pinterest:ktlint:$ktlintVersion"
}

android {
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        main.kotlin.srcDirs += 'src/main/java'
        main.java.srcDirs += 'src/main/java'

        test {

        }
    }
    testOptions {
        unitTests {
            // Note: without this, all Robolectric tests using BuildConfig will fail.
            includeAndroidResources = true
            all {
                maxHeapSize = "1024m"
            }
        }

        kotlinOptions {
            freeCompilerArgs += ["-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"]
        }
    }

    buildFeatures {
        compose = true
        viewBinding true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "$androidxComposeCompilerVersion"
    }
}

ext {
    artifactId = "payments-core"
    artifactName = "payments-core"
    artifactDescrption = "The core module of Stripe Payment Android SDK"
}

apply from: "${rootDir}/deploy/deploy.gradle"
