plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'checkstyle'
    id 'org.jetbrains.dokka'
    id 'org.jetbrains.kotlin.plugin.parcelize'
}

assemble.dependsOn('lint')
check.dependsOn('checkstyle')

configurations {
    javadocDeps
    ktlint
}

if (System.getenv("JITPACK")) {
    group='com.github.stripe.stripe-android'
}

dependencies {
    api project(":stripe-core")

    implementation "androidx.annotation:annotation:$androidxAnnotationVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.browser:browser:$androidxBrowserVersion"
    implementation "androidx.recyclerview:recyclerview:$androidxRecyclerviewVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$androidxLifecycleVersion"
    implementation "androidx.fragment:fragment-ktx:$androidxFragmentVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintlayoutVersion"
    implementation "androidx.activity:activity-ktx:$androidxActivityVersion"
    implementation "com.google.android.gms:play-services-wallet:$playServicesWalletVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    implementation "androidx.activity:activity-compose:$androidxActivityVersion"
    implementation 'com.google.android.instantapps:instantapps:1.1.0'

    // For instructions on replacing the BouncyCastle dependency used by the 3DS2 SDK, see
    // https://github.com/stripe/stripe-android/issues/3173#issuecomment-785176608
    implementation "com.stripe:stripe-3ds2-android:6.1.4"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"

    javadocDeps "androidx.annotation:annotation:$androidxAnnotationVersion"
    javadocDeps "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    javadocDeps "com.google.android.material:material:$materialVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "androidx.test:core:$androidTestVersion"
    testImplementation "org.json:json:$jsonVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlinVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
    testImplementation "com.google.truth:truth:$truthVersion"
    testImplementation "androidx.arch.core:core-testing:$androidxArchCoreVersion"
    testImplementation "androidx.fragment:fragment-testing:$androidxFragmentVersion"

    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestUtil "androidx.test:orchestrator:$androidTestVersion"

    ktlint "com.pinterest:ktlint:$ktlintVersion"
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.compileSdkVersion
        consumerProguardFiles 'proguard-rules.txt'

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // From https://developer.android.com/training/testing/junit-runner:
        // > To remove all shared state from your device's CPU and memory after each test,
        // > use the clearPackageData flag.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        main.kotlin.srcDirs += 'src/main/java'
        main.java.srcDirs += 'src/main/java'

        test {

        }
    }
    testOptions {
        unitTests {
            // Note: without this, all Robolectric tests using BuildConfig will fail.
            includeAndroidResources = true
            all {
                maxHeapSize = "1024m"
            }
        }
    }
    productFlavors {
    }

    lintOptions {
        enable "Interoperability"
        disable "CoroutineCreationDuringComposition"
        lintConfig file("../settings/lint.xml")
    }

    dokkaHtml {
        outputDirectory = new File("${project.rootDir}/docs/$project.name")
    }

    buildFeatures {
        compose = true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    composeOptions {
        kotlinCompilerExtensionVersion "$androidxComposeVersion"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}


ext {
    artifactId = "payments-core"
    artifactName = "payments-core"
    artifactDescrption = "The core module of Stripe Payment Android SDK"
}

apply from: "${rootDir}/deploy/deploy.gradle"
