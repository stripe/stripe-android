apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
// make sure this line comes *after* you apply the Android plugin
apply plugin: 'com.getkeepsafe.dexcount'

assemble.dependsOn('lint')
check.dependsOn('checkstyle')

version = VERSION_NAME

configurations {
    javadocDeps
}

dependencies {
    implementation "androidx.annotation:annotation:${rootProject.ext.androidxAnnotationVersion}"
    implementation "androidx.appcompat:appcompat:${rootProject.ext.androidxAppCompatVersion}"
    // Api for this import because we use reflection to alter TextInputLayout
    api "com.google.android.material:material:${rootProject.ext.androidxMaterialVersion}"

    javadocDeps "androidx.annotation:annotation:${rootProject.ext.androidxAnnotationVersion}"
    javadocDeps "androidx.appcompat:appcompat:${rootProject.ext.androidxAppCompatVersion}"
    javadocDeps "com.google.android.material:material:${rootProject.ext.androidxMaterialVersion}"
    compileOnly "javax.annotation:jsr250-api:${rootProject.ext.javaAnnotationVersion}"

    testImplementation "junit:junit:${rootProject.ext.junitVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.ext.mockitoCoreVersion}"
    testImplementation "org.robolectric:robolectric:${rootProject.ext.robolectricVersion}"
    testImplementation 'androidx.test:core:1.1.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test:runner:1.1.1'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion rootProject.ext.compileSdkVersion
        consumerProguardFiles 'proguard-rules.txt'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        test {

        }
    }
    testOptions {
        unitTests {
            // Note: without this, all Robolectric tests using BuildConfig will fail.
            includeAndroidResources = true
        }
    }
    productFlavors {
    }
}

apply from: 'deploy.gradle'
