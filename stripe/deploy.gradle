apply plugin: "signing"
apply plugin: "maven-publish"

def getReleaseRepositoryUrl() {
    return findProperty('RELEASE_REPOSITORY_URL') ?:
            "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return findProperty('SNAPSHOT_REPOSITORY_URL') ?:
            "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return findProperty('NEXUS_USERNAME') ?: ""
}

def getRepositoryPassword() {
    return findProperty('NEXUS_PASSWORD') ?: ""
}

task androidJavadocs(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath = configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate { project ->
    // See https://developer.android.com/studio/build/maven-publish-plugin
    // and https://docs.gradle.org/current/userguide/publishing_maven.html
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // Adds Javadocs and Sources as separate jars.
                artifact androidJavadocsJar
                artifact androidSourcesJar

                groupId = "com.stripe"
                artifactId = "stripe-android"
                version = VERSION_NAME

                pom {
                    name = "stripe-android"
                    packaging = "aar"
                    description = "Stripe Android SDK"
                    url = "https://github.com/stripe/stripe-android"

                    scm {
                        url = "https://github.com/stripe/stripe-android"
                        connection = "scm:org-856813@github.com:stripe/stripe-android.git"
                        developerConnection = "scm:org-856813@github.com:stripe/stripe-android.git"
                    }

                    licenses {
                        license {
                            name = "The MIT License"
                            url = "https://raw.githubusercontent.com/stripe/stripe-android/master/LICENSE"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "stripe"
                            name = "Stripe"
                        }
                    }
                }
            }
            // Creates a Maven publication called "debug".
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = "com.stripe"
                artifactId = "stripe-android"
                version = VERSION_NAME

                pom {
                    name = "stripe-android"
                    packaging = "aar"
                    description = "Stripe Android SDK"
                    url = "https://github.com/stripe/stripe-android"

                    scm {
                        url = "https://github.com/stripe/stripe-android"
                        connection = "scm:org-856813@github.com:stripe/stripe-android.git"
                        developerConnection = "scm:org-856813@github.com:stripe/stripe-android.git"
                    }

                    licenses {
                        license {
                            name = "The MIT License"
                            url = "https://raw.githubusercontent.com/stripe/stripe-android/master/LICENSE"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "stripe"
                            name = "Stripe"
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url getReleaseRepositoryUrl()
                credentials {
                    username = getRepositoryUsername()
                    password = getRepositoryPassword()
                }
            }
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("publish") }
        useGpgCmd()
        sign publishing.publications.release
    }

    tasks.withType(Sign) {
        onlyIf { project.hasProperty('signing.gnupg.keyName') }
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
