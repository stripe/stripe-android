/**
 * Base build configuration for all android example applications (modules applying the android application plugin).
 *
 * Don't add any configuration here that is not common to *every* android example application.
 */
apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode 11
        versionName VERSION_NAME
    }

    signingConfigs {
        config {
            keyAlias 'business'
            keyPassword 'Stripe12345'
            storeFile rootProject.file('stripe-example.keystore')
            storePassword 'Stripe12345'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }

    testOptions {
        // Make sure animations are off when we run espresso tests
        animationsDisabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of("11"))
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}
