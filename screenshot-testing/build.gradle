apply from: configs.androidLibrary
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

configurations.configureEach {
    if (!name.toLowerCase().contains('androidtest')) {
        resolutionStrategy {
            force testLibs.layoutlib
        }
    } else {
        // Exclude layoutlib from androidTest configurations to prevent dexing errors
        // This module is only used for unit tests (Paparazzi), not instrumentation tests
        exclude group: 'com.android.tools.layoutlib', module: 'layoutlib'
    }
    exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
}

dependencies {
    implementation project(':stripe-ui-core')
    implementation libs.compose.ui
    implementation libs.compose.foundation
    implementation libs.compose.material3

    implementation testLibs.kotlin.reflect

    // Implemented here instead of using plugin which implements with testImplementation
    api("app.cash.paparazzi:paparazzi:${versions.paparazzi}") {
        exclude group: 'jakarta.activation'
        exclude group: 'androidx.annotation', module: 'annotation-jvm'
        exclude group: 'com.android.tools.ddms', module: 'ddmlib'
    }
}

android {
    buildFeatures {
        compose = true
    }

    packagingOptions {
        resources {
            pickFirsts += [
                    'META-INF/AL2.0',
                    'META-INF/LGPL2.1',
                    'META-INF/DEPENDENCIES',
                    'META-INF/LICENSE.md',
                    'META-INF/NOTICE.md',
                    'xsd/catalog.xml'
            ]
        }
    }
}
