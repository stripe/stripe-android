import java.time.Duration

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'dependencies.gradle'
    apply from: './build-configuration/build-environment.gradle'
    ext.kotlinVersion = '1.8.0'
    // until Dokka 1.8.0 is released and we no longer need dev builds
    ext.dokkaVersion = '1.8.0-dev-194'

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        // until Dokka 1.8.0 is released and we no longer need dev builds
        maven { url "https://maven.pkg.jetbrains.space/kotlin/p/dokka/dev" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:0.13.0"
        classpath 'com.karumi:shot:5.14.1'
        classpath 'app.cash.paparazzi:paparazzi-gradle-plugin:1.2.0'
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.google.firebase:firebase-appdistribution-gradle:4.0.0'
        classpath buildLibs.detektGradlePlugin
    }
}

plugins {
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'io.codearte.nexus-staging' version '0.30.0'
    id 'com.google.devtools.ksp' version '1.8.0-1.0.9' apply false
}

apply plugin: "io.gitlab.arturbosch.detekt"

allprojects {
    group = GROUP

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        // until Dokka 1.8.0 is released and we no longer need dev builds
        maven { url "https://maven.pkg.jetbrains.space/kotlin/p/dokka/dev" }
    }
}

ext {
    buildToolsVersion = "30.0.3"
    compileSdkVersion = 33

    androidxActivityVersion = '1.6.1'
    androidxAnnotationVersion = '1.6.0'
    androidxAppcompatVersion = '1.6.1'
    androidxArchCoreVersion = '2.2.0'
    androidxBrowserVersion = '1.5.0'
    androidxComposeVersion = '1.4.1'
    androidxComposeRuntimeVersion = '1.4.1'
    androidxComposeUiVersion = '1.4.1'
    androidxComposeCompilerVersion = '1.4.1'
    androidxConstraintlayoutComposeVersion = '1.0.1'
    androidxConstraintlayoutVersion = '2.1.4'
    androidxCoreVersion = '1.9.0'
    androidxFragmentVersion = '1.5.5'
    androidxLifecycleVersion = '2.5.1'
    androidxNavigationVersion = '2.5.3'
    androidxPreferenceVersion = '1.2.0'
    androidxRecyclerviewVersion = '1.2.1'
    androidxSecurityVersion = '1.1.0-alpha05'

    kotlinCoroutinesVersion = '1.6.4'
    kotlinSerializationVersion = '1.5.0'
    accompanistVersion = '0.28.0'
    ktlintVersion = '0.48.2'
    materialVersion = '1.8.0'
    daggerVersion = '2.45'
    playServicesWalletVersion = '19.1.0'
    placesVersion = '3.0.0'
    playServicesVersion = '1.6.4'
    leakCanaryVersion = '2.10'
    okhttpVersion = '4.10.0'
    stripe3ds2Version = '6.1.7'
    retrofitVersion = '2.9.0'
    gsonVersion = '2.10.1'

    androidTestVersion = '1.5.0'
    androidTestRunnerVersion = '1.5.2'
    androidTestOrchestratorVersion = '1.4.2'
    androidTestJunitVersion = '1.1.5'
    truthVersion = '1.1.3'
    junitVersion = '4.13.2'
    robolectricVersion = '4.10'
    mockitoCoreVersion = '5.2.0'
    mockitoKotlinVersion = '4.1.0'
    espressoVersion = '3.5.1'
    jsonVersion = '20230227'
    uiautomator = '2.2.0'
    showkaseVersion = '1.0.0-beta18'

    group_name = GROUP
    version_name = VERSION_NAME
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

nexusPublishing {
    packageGroup = GROUP

    repositories {
        sonatype {
            username = project.findProperty('NEXUS_USERNAME') ?: ""
            password = project.findProperty('NEXUS_PASSWORD') ?: ""
        }
    }

    clientTimeout = Duration.ofMinutes(5)
    connectTimeout = Duration.ofMinutes(1)

    transitionCheckOptions {
        maxRetries.set(40)
        delayBetween.set(Duration.ofSeconds(10))
    }
}

apply plugin: 'binary-compatibility-validator'
apply plugin: 'org.jetbrains.dokka'


tasks.dokkaHtmlMultiModule.configure {
    outputDirectory = new File("${project.rootDir}/docs")
}

apiValidation {
    ignoredPackages += ["com.stripe.android.databinding"]
    ignoredProjects += [
            "example",
            "financial-connections-example",
            "identity-example",
            "network-testing",
            "payments-core-testing",
            "paymentsheet-example",
            "stripecardscan-example",
    ]
    nonPublicMarkers.add("androidx.annotation.RestrictTo")
    nonPublicMarkers.add("com.stripe.android.ExperimentalPaymentSheetDecouplingApi")
    nonPublicMarkers.add("dagger.internal.DaggerGenerated")
}
