// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlinVersion = '1.3.60'
    ext.dokkaVersion = '0.9.17' // 0.9.18 has a bug that causes the task to fail

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokkaVersion"
    }
}

allprojects {
    group = GROUP

    repositories {
        google()
        jcenter()
    }

    task checkstyle(type: Checkstyle) {
        showViolations = true
        configFile file("../settings/checkstyle.xml")
        source 'src/main/java'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'

        // empty classpath
        classpath = files()
    }
}

ext {
    buildToolsVersion = "29.0.2"
    compileSdkVersion = 29

    javaAnnotationVersion = '1.0'
    kotlinVersion = '1.3.61'
    kotlinCoroutinesVersion = '1.3.0'
    espressoVersion = '3.2.0'
    ktlintVersion = '0.36.0'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

apply plugin: "io.codearte.nexus-staging"

nexusStaging {
    username = project.hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
    password = project.hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
    packageGroup = GROUP
}
