// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlinVersion = '1.4.30'
    ext.dokkaVersion = '1.4.0-rc'

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:2.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:0.4.0"
    }
}

plugins {
    id 'io.codearte.nexus-staging' version '0.22.0'
}

allprojects {
    group = GROUP

    repositories {
        google()
        jcenter()
    }

    task checkstyle(type: Checkstyle) {
        showViolations = true
        configFile file("../settings/checkstyle.xml")
        source 'src/main/java'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'

        // empty classpath
        classpath = files()
    }
}

ext {
    buildToolsVersion = "30.0.3"
    compileSdkVersion = 30

    kotlinCoroutinesVersion = '1.4.2'
    androidLifecycleVersion = '2.3.0'
    espressoVersion = '3.3.0'
    ktlintVersion = '0.40.0'
    materialVersion = '1.3.0'

    fragmentVersion = '1.3.0'

    androidTestVersion = '1.3.0'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

nexusStaging {
    username = project.hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
    password = project.hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
    packageGroup = GROUP
}

apply plugin: 'binary-compatibility-validator'

apiValidation {
    ignoredPackages += ["com.stripe.android.databinding"]
    ignoredProjects += ["example"]
}
