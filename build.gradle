import java.time.Duration

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlinVersion = '1.5.31'
    ext.dokkaVersion = '1.6.10'

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:0.8.0"
    }
}

plugins {
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

allprojects {
    group = GROUP

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    task checkstyle(type: Checkstyle) {
        showViolations = true
        configFile file("../settings/checkstyle.xml")
        source 'src/main/java'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'

        // empty classpath
        classpath = files()
    }
}

ext {
    buildToolsVersion = "30.0.3"
    compileSdkVersion = 31

    kotlinCoroutinesVersion = '1.6.0'
    kotlinSerializationVersion = '1.3.2'
    coreKtxVersion = '1.7.0'
    composeVersion = '1.0.5'
    composeActivityVersion = '1.4.0'
    espressoVersion = '3.4.0'
    ktlintVersion = '0.43.2'
    materialVersion = '1.5.0'
    daggerVersion = '2.40.5'

    androidTestVersion = '1.4.0'
    junitVersion = '4.13.2'
    robolectricVersion = '4.7.3'
    mockitoCoreVersion = '4.2.0'
    mockitoKotlinVersion = '4.0.0'

    group_name = GROUP
    version_name = VERSION_NAME
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

nexusPublishing {
    packageGroup = GROUP

    repositories {
        sonatype {
            username = project.findProperty('NEXUS_USERNAME') ?: ""
            password = project.findProperty('NEXUS_PASSWORD') ?: ""
        }
    }

    clientTimeout = Duration.ofMinutes(5)
    connectTimeout = Duration.ofMinutes(1)

    transitionCheckOptions {
        maxRetries.set(40)
        delayBetween.set(Duration.ofSeconds(10))
    }
}

apply plugin: 'binary-compatibility-validator'
apply plugin: 'org.jetbrains.dokka'


tasks.dokkaHtmlMultiModule.configure {
    outputDirectory = new File("${project.rootDir}/docs")
}

apiValidation {
    ignoredPackages += ["com.stripe.android.databinding"]
    ignoredProjects += ["example", "paymentsheet-example", "stripecardscan-example"]
}
