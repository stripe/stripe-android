import java.time.Duration

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'dependencies.gradle'
    apply from: './build-configuration/build-environment.gradle'
    ext.kotlinVersion = '1.7.20'
    ext.dokkaVersion = '1.7.20'

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:0.12.1"
        classpath 'com.karumi:shot:5.14.1'
        classpath 'dev.chrisbanes.paparazzi:paparazzi-gradle-plugin:1.1.0-sdk33-alpha04'
        classpath 'app.cash.paparazzi:paparazzi-gradle-plugin:1.2.0'
        classpath 'com.google.gms:google-services:4.3.14'
        classpath 'com.google.firebase:firebase-appdistribution-gradle:3.1.1'
        classpath buildLibs.detektGradlePlugin
    }
}

plugins {
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

apply plugin: "io.gitlab.arturbosch.detekt"

allprojects {
    group = GROUP

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }
}

ext {
    buildToolsVersion = "30.0.3"
    compileSdkVersion = 33

    androidxActivityVersion = '1.6.1'
    androidxAnnotationVersion = '1.5.0'
    androidxAppcompatVersion = '1.5.1'
    androidxArchCoreVersion = '2.1.0'
    androidxBrowserVersion = '1.4.0'
    androidxComposeVersion = '1.3.1'
    androidxComposeCompilerVersion = '1.3.2'
    androidxConstraintlayoutComposeVersion = '1.0.1'
    androidxConstraintlayoutVersion = '2.1.4'
    androidxCoreVersion = '1.9.0'
    androidxFragmentVersion = '1.5.4'
    androidxLifecycleVersion = '2.5.1'
    androidxNavigationVersion = '2.5.3'
    androidxPreferenceVersion = '1.2.0'
    androidxRecyclerviewVersion = '1.2.1'
    androidxSecurityVersion = '1.1.0-alpha04'

    kotlinCoroutinesVersion = '1.6.4'
    kotlinSerializationVersion = '1.4.1'
    accompanistVersion = '0.27.0'
    ktlintVersion = '0.47.1'
    // material 1.6 causes paymentsheet to not render correctly.
    // see here: https://github.com/material-components/material-components-android/issues/2702
    materialVersion = '1.5.0'
    daggerVersion = '2.44.2'
    playServicesWalletVersion = '19.1.0'
    placesVersion = '2.7.0'
    playServicesVersion = '1.6.4'
    leakCanaryVersion = '2.10'
    okhttpVersion = '4.10.0'

    androidTestVersion = '1.5.0'
    androidTestOrchestratorVersion = '1.4.2'
    androidTestJunitVersion = '1.1.5'
    truthVersion = '1.1.3'
    junitVersion = '4.13.2'
    robolectricVersion = '4.9.2'
    mockitoCoreVersion = '4.9.0'
    mockitoKotlinVersion = '4.1.0'
    espressoVersion = '3.5.0'
    jsonVersion = '20220924'
    uiautomator = '2.2.0'
    showkaseVersion = '1.0.0-beta14'

    group_name = GROUP
    version_name = VERSION_NAME
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

nexusPublishing {
    packageGroup = GROUP

    repositories {
        sonatype {
            username = project.findProperty('NEXUS_USERNAME') ?: ""
            password = project.findProperty('NEXUS_PASSWORD') ?: ""
        }
    }

    clientTimeout = Duration.ofMinutes(5)
    connectTimeout = Duration.ofMinutes(1)

    transitionCheckOptions {
        maxRetries.set(40)
        delayBetween.set(Duration.ofSeconds(10))
    }
}

apply plugin: 'binary-compatibility-validator'
apply plugin: 'org.jetbrains.dokka'


tasks.dokkaHtmlMultiModule.configure {
    outputDirectory = new File("${project.rootDir}/docs")
}

apiValidation {
    ignoredPackages += ["com.stripe.android.databinding"]
    ignoredProjects += ["example", "paymentsheet-example", "stripecardscan-example", "identity-example", "financial-connections-example"]
    nonPublicMarkers.add("androidx.annotation.RestrictTo")
    nonPublicMarkers.add("dagger.internal.DaggerGenerated")
}
