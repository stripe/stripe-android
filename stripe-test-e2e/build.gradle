apply from: configs.androidApplication
apply plugin: 'com.google.devtools.ksp'

// Read values from gradle.properties or system environment variable
def getBackendUrl() {
    return readProperty('STRIPE_END_TO_END_TESTS_BACKEND_URL')
}

def getPublishableKey() {
    return readProperty('STRIPE_END_TO_END_TESTS_PUBLISHABLE_KEY')
}

private def readProperty(name) {
    final String propValue = findProperty(name) ?: System.getenv(name)
    return propValue?.trim() ? propValue : ""
}

android {
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 1

        // From https://developer.android.com/training/testing/junit-runner:
        // > To remove all shared state from your device's CPU and memory after each test,
        // > use the clearPackageData flag.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        manifestPlaceholders = [
                BACKEND_URL: getBackendUrl(),
                PUBLISHABLE_KEY: getPublishableKey()
        ]
    }

    testOptions {
        unitTests {
            // Note: without this, all Robolectric tests using BuildConfig will fail.
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation project(':payments')

    ksp libs.moshiKotlinCodegen

    testImplementation libs.kotlin.coroutines
    testImplementation libs.kotlin.coroutinesAndroid
    testImplementation libs.retrofit
    testImplementation libs.retrofitMoshiConverter
    testImplementation testLibs.androidx.archCore
    testImplementation testLibs.androidx.core
    testImplementation testLibs.androidx.junitKtx
    testImplementation testLibs.junit
    testImplementation testLibs.kotlin.annotations
    testImplementation testLibs.kotlin.coroutines
    testImplementation testLibs.kotlin.junit
    testImplementation testLibs.robolectric
    testImplementation testLibs.truth
}
