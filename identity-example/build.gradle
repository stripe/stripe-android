plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

assemble.dependsOn('lint')
check.dependsOn('checkstyle')

configurations {
    ktlint
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.stripe.android.identity.example"
        minSdk 21
        targetSdk rootProject.ext.compileSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    flavorDimensions += "theme"
    productFlavors {
        create("theme1") {
            manifestPlaceholders["appTheme"] = "@style/Theme.Theme1"
            manifestPlaceholders["appIcon"] = "@drawable/merchant_logo_purple"
            manifestPlaceholders["appIconRound"] = "@drawable/merchant_logo_purple"

            dimension = "theme"
            applicationIdSuffix = ".theme1"
            versionNameSuffix = "-theme1"
        }
        create("theme2") {
            manifestPlaceholders["appTheme"] = "@style/Theme.Theme2"
            manifestPlaceholders["appIcon"] = "@drawable/merchant_logo_red"
            manifestPlaceholders["appIconRound"] = "@drawable/merchant_logo_red"

            dimension = "theme"
            applicationIdSuffix = ".theme2"
            versionNameSuffix = "-theme2"
        }
    }
}

dependencies {
    implementation project(':identity')

    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation 'com.github.kittinunf.fuel:fuel:2.3.1'
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintlayoutVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinSerializationVersion"
    implementation "androidx.browser:browser:$androidxBrowserVersion"

    testImplementation "junit:junit:$junitVersion"

    androidTestImplementation "androidx.test.ext:junit:$androidTestJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"


    ktlint "com.pinterest:ktlint:$ktlintVersion"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

