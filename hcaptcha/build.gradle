import groovy.text.SimpleTemplateEngine
import java.util.stream.Collectors

apply from: configs.androidLibrary

apply plugin: 'checkstyle'
apply plugin: "org.jetbrains.kotlin.plugin.parcelize"
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'

dependencies {
    implementation libs.androidx.appCompat
    implementation libs.androidx.core
    implementation libs.kotlin.serialization

    testImplementation testLibs.junit
    testImplementation testLibs.kotlin.test
    testImplementation testLibs.mockito.inline
    testImplementation testLibs.mockito.kotlin
    testImplementation testLibs.robolectric
    testImplementation testLibs.truth
}

android {
    buildFeatures {
        buildConfig true
    }

    testOptions {
        animationsDisabled = true
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
}

// Generate the HTML class for hCaptcha
def hcaptchaPackageName = "com.stripe.hcaptcha"
def hcaptchaPackagePath = hcaptchaPackageName.replaceAll('\\.', '/')
def hcaptchaGenBaseDir = project.layout.buildDirectory.dir("generated/source/hcaptcha/main")
def hcaptchaGenDir = project.layout.buildDirectory.dir("generated/source/hcaptcha/main/${hcaptchaPackagePath}")
def templateFile = project.layout.projectDirectory.file("src/main/html/HCaptchaHtml.java.tml")
def htmlFile = project.layout.projectDirectory.file("src/main/html/hcaptcha.html")

def generateHtmlClass = tasks.register("generateJavaClassFromStaticHtml") {
    group 'Generate'
    description "Generate HTML java class"

    inputs.file(templateFile)
    inputs.file(htmlFile)
    outputs.dir(hcaptchaGenDir)

    doFirst {
        def html = htmlFile.getAsFile().readLines()
                .stream()
                .collect(Collectors.joining("\n"))

        def engine = new SimpleTemplateEngine()
        def src = engine.createTemplate(templateFile.getAsFile().text).make([
                "htmlContent": html,
                "packageName": hcaptchaPackageName
        ])

        def outDir = hcaptchaGenDir.get().asFile
        outDir.mkdirs()
        new File(outDir, "HCaptchaHtml.kt").write(src.toString())
    }
}

android.sourceSets.main.kotlin.srcDirs += hcaptchaGenBaseDir

tasks.configureEach {
    if (name.startsWith("compile") && name.contains("Kotlin")) {
        dependsOn(generateHtmlClass)
    }
}

ext {
    artifactId = "hcaptcha"
    artifactName = "hcaptcha"
    artifactDescrption = "The hcaptcha module of Stripe Payment Android SDK"
}

apply from: "${rootDir}/deploy/deploy.gradle"
