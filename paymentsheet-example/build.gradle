apply from: configs.androidApplication

apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'shot'

// Read values from gradle.properties or system environment variable
def getBackendUrl() {
    return findProperty('STRIPE_PAYMENTSHEET_EXAMPLE_BACKEND_URL') ?: ""
}

def getGooglePlacesApiKey() {
    return findProperty('STRIPE_PAYMENTSHEET_EXAMPLE_GOOGLE_PLACES_API_KEY') ?: ""
}

dependencies {
    implementation project(':payments')
    implementation project(':stripecardscan')
    implementation project(':financial-connections')

    implementation "com.google.android.libraries.places:places:$placesVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxLifecycleVersion"
    implementation "androidx.preference:preference-ktx:$androidxPreferenceVersion"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintlayoutVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidxNavigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$androidxNavigationVersion"
    implementation "androidx.activity:activity-ktx:$androidxActivityVersion"
    implementation "com.google.android.material:material:$materialVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinSerializationVersion"

    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:1.0.0"
    implementation 'com.github.kittinunf.fuel:fuel:2.3.1'
    implementation 'com.godaddy.android.colorpicker:compose-color-picker-android:0.7.0'

    implementation "androidx.compose.ui:ui:$androidxComposeUiVersion"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:$androidxComposeUiVersion"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation:$androidxComposeVersion"
    // Material Design
    implementation "androidx.compose.material:material:$androidxComposeVersion"
    // Material design icons
    implementation "androidx.compose.material:material-icons-core:$androidxComposeVersion"
    implementation "androidx.compose.material:material-icons-extended:$androidxComposeVersion"
    // Integration with activities
    implementation "androidx.activity:activity-compose:$androidxActivityVersion"
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$androidxLifecycleVersion"
    // Integration with observables
    implementation "androidx.compose.runtime:runtime-livedata:$androidxComposeRuntimeVersion"
    androidTestImplementation 'com.google.android.gms:play-services-wallet:19.1.0'
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$uiautomator"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidTestJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"

    // Core library
    androidTestImplementation "androidx.test:core:$androidTestVersion"
    androidTestImplementation 'androidx.test:core-ktx:1.5.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:$androidTestRunnerVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"

    // Assertions
    androidTestImplementation "androidx.test.ext:junit:$androidTestJunitVersion"
    androidTestImplementation "androidx.test.ext:truth:$androidTestVersion"
    androidTestImplementation "com.google.truth:truth:$truthVersion"
    androidTestImplementation "androidx.arch.core:core-testing:$androidxArchCoreVersion"

    // Espresso dependencies
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$espressoVersion") {
        exclude group: 'org.checkerframework', module: 'checker'
    }
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation("androidx.test.espresso:espresso-accessibility:$espressoVersion") {
        exclude group: 'org.checkerframework', module: 'checker'
    }
    androidTestImplementation "androidx.test.espresso:espresso-web:$espressoVersion"

    // Test rules and transitive dependencies:
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$androidxComposeUiVersion"
    androidTestImplementation project(':payments-ui-core')
    androidTestImplementation project(':payments-core-testing')
    // Needed for createComposeRule, but not createAndroidComposeRule:
    debugImplementation "androidx.compose.ui:ui-test-manifest:$androidxComposeUiVersion"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
}

shot {
    tolerance = 1 // 1% tolerance because screenshots look slightly different on arm64-v8a vs x86_64 vs m1 emulators
}

android {
    defaultConfig {
        applicationId "com.stripe.android.paymentsheet.example"
        versionCode 11

        testInstrumentationRunner "com.karumi.shot.ShotTestRunner"

        manifestPlaceholders = [
                BACKEND_URL: getBackendUrl(),
                GOOGLE_PLACES_API_KEY: getGooglePlacesApiKey()
        ]
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$androidxComposeCompilerVersion"
    }
}
