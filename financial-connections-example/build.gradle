apply from: configs.androidApplication

apply plugin: 'com.emergetools.android'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

def testEnvironment = System.getenv("test_environment") ?: "production"

emerge {
    // Api token is implicitly set to the EMERGE_API_TOKEN env variable

    size {
        tag.set(System.getenv("EMERGE_TAG"))
    }

    vcs {
        gitHub {
            repoOwner.set("stripe")
            repoName.set("stripe-android")
        }
    }
}

android {
    defaultConfig {
        applicationId "com.stripe.android.financialconnections.example"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        versionName VERSION_NAME
        versionCode getVersionCode(versionName)

        buildConfigField "String", "TEST_ENVIRONMENT", "\"${testEnvironment}\""

        manifestPlaceholders = [
            STRIPE_FINANCIAL_CONNECTIONS_EXAMPLE_BACKEND_URL: getBackendUrl()
        ]
    }

    buildTypes {
        debug {
            firebaseAppDistribution {
                groups = "financial-connections"
            }
        }
    }

    buildFeatures {
        compose true
    }
}

// Read values from gradle.properties or system environment variable
def getBackendUrl() {
    return findProperty('STRIPE_FINANCIAL_CONNECTIONS_EXAMPLE_BACKEND_URL') ?: ""
}

dependencies {
    implementation project(':financial-connections')
    implementation project(':payments-core')
    implementation project(':paymentsheet')

    implementation libs.androidx.activity
    implementation libs.androidx.appCompat
    implementation libs.androidx.browser
    implementation libs.androidx.coreKtx
    implementation libs.androidx.lifecycle
    implementation libs.androidx.liveDataKtx
    implementation libs.androidx.workManager
    implementation libs.compose.activity
    implementation libs.compose.material
    implementation libs.compose.materialIcons
    implementation libs.compose.liveData
    implementation libs.compose.ui
    implementation libs.compose.uiToolingPreview
    implementation libs.loggingInterceptor
    implementation libs.material
    implementation libs.retrofit
    implementation libs.retrofitKotlinSerializationConverter
    implementation libs.kotlin.serialization
    implementation libs.zxing

    debugImplementation libs.compose.uiTestManifest
    debugImplementation libs.compose.uiTooling
    debugImplementation libs.leakCanary

    testImplementation testLibs.androidx.junit
    testImplementation testLibs.junit

    androidTestImplementation testLibs.espresso.core
    androidTestImplementation testLibs.androidx.composeUi
}
